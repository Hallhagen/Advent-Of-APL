main2 ← {
   input ← ↑loadData 3
   input ← ⊃¨('[=\-\+\&\#\%\/\@\$]' ⎕R '.')¨ input
   input[⍸'*'=input] ← (+/+/'*'=input)⍴⎕UCS 300+⍳1000
   windows33 ← ,⊂⍤⊢⌺3 3 ⊢ input
   windows34 ← ,⊂⍤⊢⌺3 4 ⊢ input
   windows35 ← ,⊂⍤⊢⌺3 5 ⊢ input
   w ← windows33,windows34,windows35
   firstRow ← 1 ⌷¨ w
   lastRow ← 1 ⌷¨⊖¨ w
   firstCol ← 1 ⌷¨⍉¨ w
   lastCol ← 1 ⌷¨⊖¨⍉¨ w
   allSurroundingElements ← (' ' ⎕R '.')¨firstRow,¨lastRow,¨firstCol,¨lastCol
   surroundingElementsHaveNumbers ← ∨/¨'*' = ('\d' ⎕R '*')¨ allSurroundingElements
   surroundingElementsHaveLetters ← ∨/¨'*' = ('[^0-9\.]' ⎕R '*')¨ allSurroundingElements
   surroundingElementLetters ← ∪¨('[0-9\.]' ⎕R '')¨ allSurroundingElements
   centerElements ← ⍉¨⊖¨1↓¨⊖¨1↓¨⍉¨⊖¨1↓¨⊖¨1↓¨w
   centerElementsAreNumbers ← ∧/¨∧/¨','=↑¨('[0-9]' ⎕R ',')¨↓¨centerElements

   numberIsValidAndHasLetter ← surroundingElementsHaveLetters ∧ centerElementsAreNumbers ∧ ~surroundingElementsHaveNumbers

   numbers ← ⍎¨,¨ numberIsValidAndHasLetter / centerElements
   letters ← numberIsValidAndHasLetter / surroundingElementLetters

   grouped ← letters {⊂⍵}⌸ numbers
   grouped ← grouped[⍸⊃¨1≠⍴¨grouped]

   +/×/¨grouped
}
